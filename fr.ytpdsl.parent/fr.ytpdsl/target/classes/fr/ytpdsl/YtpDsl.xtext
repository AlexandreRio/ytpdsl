grammar fr.ytpdsl.YtpDsl with org.eclipse.xtext.common.Terminals

generate ytpDsl "http://www.ytpdsl.fr/YtpDsl"

YtpModel:
	(information=GeneratorInformation)?
	medias+=Media+;

GeneratorInformation:
	{GeneratorInformation}
	('@author' authorName=STRING)
	('@version' version=STRING)?
	('@creation' creationDate=STRING)?
	('FFMPEG_PATH' ffmpeg=STRING)
	('FFPROBE_PATH' ffprobe=STRING)
	library+=VideoLibrary*
;

VideoLibrary:
	'folder' folder=STRING 'ext' (extension+=STRING)+
;

Media:
	'media' description=MediaDescription
;

MediaDescription:
	(ImageDescription | VideoDescription);

ImageDescription:
	'image' (imageid=ID)? location=STRING
	(LEFT_BRACKET
	('toptext' top=STRING)
	('bottomtext' bottom=STRING)
	RIGHT_BRACKET)?;

VideoDescription:
	'videoseq' (videoid=ID)? location=STRING
	(LEFT_BRACKET
	('duration' duration=INT)?
	('probability' probability=INT)? // percentage // PERCENTAGE 
	// (note: probability does not make any sense for mandatory medias... 
	// we can refactor the grammar or handle this situation with a model transformation) 
	('description' description=STRING)?
	('filter' filter=Filter)? // apply some filters to a video
	('text' text=VideoText)? // incrust a text into a video
	RIGHT_BRACKET)?;

VideoText:
	LEFT_BRACKET
	'content' content=STRING
	'position' position=Position
	('color' color=STRING)?
	('size' size=INT)?
	RIGHT_BRACKET;

Position:
	'TOP' | 'BOTTOM' | 'CENTER';

Filter:
	FlipFilter | NegateFilter | BlackWhiteFilter;

BlackWhiteFilter:
	{BlackWhiteFilter}
	'b&w';

NegateFilter:
	{NegateFilter}
	'negate';

FlipFilter:
	'flip' orientation=('h' | 'horizontal' | 'v' | 'vertical');

terminal LEFT_BRACKET:
	'{';

terminal RIGHT_BRACKET:
	'}';

	